HAI 1.4
  CAN HAS STDIO?

OBTW
    Testing the copying of a bukkit
TLDR
HOW IZ I TESTIN
    I HAS A issue ITZ LIEK A problem
    issue HAS A SRS 1 ITZ 2
    issue HAS A SRS 2 ITZ 2
    issue IZ CONSUMIN YR 1337 AN YR 0 MKAY
    I HAS A child ITZ LIEK A problem
    child HAS A SRS 1 ITZ 2
    child HAS A SRS 2 ITZ 2
    child IZ CONSUMIN YR 420 AN YR 0 MKAY

    VISIBLE SMOOSH "issue: " AN issue'Z SRS 1 MKAY
    VISIBLE SMOOSH "child: " AN child'Z SRS 1 MKAY
IF U SAY SO


O HAI IM problem
    I HAS A len ITZ 0

    BTW prints opcode and switchcases execution
    HOW IZ I DOTHING YR OPCODE AN YR BASE AN YR ARG AN YR TARGET
        VISIBLE SMOOSH "Execute function DOTHING " AN OPCODE AN " " AN BASE AN " " AN ARG MKAY
        OPCODE, WTF?
            OMG 1 BTW ADD
                VISIBLE "Should add"
                I HAS A thing ITZ SUM OF ME'Z SRS BASE AN ME'Z SRS ARG
                ME'Z SRS TARGET R thing
                GTFO
            OMG 2 BTW MULTIPLY
                VISIBLE "Should multiply"
                I HAS A thing ITZ PRODUKT OF ME'Z SRS BASE AN ME'Z SRS ARG
                ME'Z SRS TARGET R thing
                GTFO
            OMG 99
                VISIBLE "Should stop"
                FOUND YR -1
            OMGWTF
                VISIBLE SMOOSH "Unexpected Opcode: " AN OPCODE MKAY
                FOUND YR -1
        OIC
        GTFO
    IF U SAY SO

    HOW IZ I CONSUMIN YR NOUN AN YR VERB
          BTW set up program alarm state
          ME'Z SRS 1 R NOUN
          ME'Z SRS 2 R VERB
        GTFO
    IF U SAY SO

    HOW IZ I PREPPIN
        BTW PARSING BUKKIT from newline delimited input
      IM IN YR LOOP UPPIN YR INDEX
          I HAS A something
          GIMMEH something
          something IS NOW A YARN
          BOTH SAEM something AN "", O RLY?
            YA RLY
              VISIBLE SMOOSH "End of input at index " AN INDEX MKAY
              ME'Z len R INDEX
              GTFO
            NO WAI
          OIC
          something IS NOW A NUMBR
          ME HAS A SRS INDEX ITZ something
      IM OUTTA YR LOOP
      GTFO
    IF U SAY SO

    HOW IZ I XCUTE
      BTW execute
      IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX ME'Z len
        I HAS A opcode ITZ ME'Z SRS INDEX
        BOTH SAEM opcode AN 99, O RLY?
            YA RLY, GTFO
        OIC

        I HAS A base ITZ ME'Z SRS SUM OF INDEX AN 1
        I HAS A arg ITZ ME'Z SRS SUM OF INDEX AN 2
        I HAS A target ITZ ME'Z SRS SUM OF INDEX AN 3
        VISIBLE SMOOSH "Should Execute: " AN opcode AN " " AN base AN " " AN arg AN " " AN target MKAY

        BTW EXECUTE
        I HAS A val
        val R ME IZ DOTHING YR opcode AN YR base AN YR arg AN YR target MKAY
        BOTH SAEM val AN -1, O RLY?
          YA RLY
            GTFO
        OIC

        INDEX R SUM OF INDEX AN 3 BTW UPP THEM INDEX
      IM OUTTA YR LOOP

       BTW return pos 0
       VISIBLE SMOOSH "Round Res: " AN ME'Z SRS 0 MKAY
       FOUND YR ME'Z SRS 0

    IF U SAY SO
KTHX

I IZ TESTIN MKAY

I HAS A SOLUTION
I HAS A LOOPY ITZ A TROOF
LOOPY R WIN
I HAS A N ITZ 0
I HAS A V ITZ 0

problem IZ PREPPIN MKAY

IM IN YR LOOP NERFIN YR MOM WILE BOTH SAEM LOOPY AN WIN
    I HAS A IDENT ITZ SMOOSH "issue" AN MOM MKAY
    I HAS A SRS IDENT ITZ LIEK A problem

    SRS IDENT IZ CONSUMIN YR N AN YR V MKAY
    VISIBLE SMOOSH "Brute: " AN N AN " " AN V MKAY
    SOLUTION R SRS IDENT IZ XCUTE MKAY
    BOTH SAEM SOLUTION AN 19690720, O RLY?
        YA RLY
            I HAS A PRO ITZ PRODUKT OF 100 AN N
            VISIBLE SMOOSH "Found: " SUM OF V AN PRO MKAY
            LOOPY R FAIL
            GTFO
    OIC

    BTW INCREMENT FIRST ONE AND THEN THE OTHER
    BOTH SAEM N AN V, O RLY?
        YA RLY, N R SUM OF N AN 1
        NO WAI, V R N
    OIC

IM OUTTA YR LOOP
VISIBLE "DONE LOOP"
KTHXBYE

OBTW
    OPCODES:
        1:  ADD
            add up next two numbers as pointers to the inputs,
            store in *num3
        2:  MULTIPLY
        99: HALT

    After operation, move forward 4 positions (opcode, input, input, output)

TLDR


